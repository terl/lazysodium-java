
/*
 * Copyright (c) Terl Tech Ltd • 14/06/19 17:54 • goterl.com
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v2.0. If a copy of the MPL was not distributed with this
 * file, you can obtain one at http://mozilla.org/MPL/2.0/.
 */

plugins {
    id 'application'
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.18.0'
    id 'signing'
    id 'biz.aQute.bnd.builder'
}

ext {
    artifactId = "lazysodium-java"
    groupId = "com.goterl"
    version = '5.2.0'
    description = "Lazysodium (Java) makes it effortless for Java " +
            "developers to get started with Libsodium's cryptography."
}

repositories {
    mavenCentral()
}


group project.ext.groupId
version = project.ext.version

mainClassName = "com.goterl.lazysodium.LazySodium"
sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources"
        }
    }
}

processResources {
    setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

test {
    useJUnitPlatform()
}

// Dependencies
dependencies {
    implementation 'net.java.dev.jna:jna:5.17.0'
    implementation 'com.goterl:resource-loader:2.1.0'
    implementation "org.slf4j:slf4j-api:2.0.17"
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.12.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-engine:1.12.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.12.2'
}

// Tasks
signing {
    // This will use the GPG Agent
    // to sign the jar
    useGpgCmd()
    sign publishing.publications
}

task sourcesJar(type: Jar, dependsOn: classes) {
    setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}


tasks.named('jar') {
    setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes('Automatic-Module-Name': 'com.goterl.lazysodium')
    }
}


task signPom(type: Sign) {
    sign project.file("$buildDir/publications/release/pom-default.xml")

    // The signing plugin does not seem to notice
    // it when the publications folder with the
    // signature has been deleted. So we always
    // create a new signature
    outputs.upToDateWhen { false }
}


artifacts {
    archives sourcesJar, javadocJar
}

void loadProps() {
    File secretPropsFile = project.rootProject.file('local.properties')
    if (secretPropsFile.exists()) {
        Properties p = new Properties()
        new FileInputStream(secretPropsFile).withCloseable { is ->
            p.load(is)
        }
        p.each { name, value ->
            ext[name] = value
        }
    }
}

loadProps()

// Return an empty string if a property
// cannot be found
String getProp(String propName) {
    if (ext.hasProperty(propName) != null) {
        return ext[propName]
    } else {
        return ""
    }
}

String getSonatypeUserName() {
    return findProperty("ossrhUsername")
}

String getSonatypePassword() {
    return findProperty("ossrhPassword")
}

Closure createPomClosure = {
    def root = asNode()
    root.appendNode('description', project.ext.description)
    root.appendNode('name', project.ext.artifactId)
    root.appendNode('url', 'https://github.com/terl/lazysodium-java')

    def scm = root.appendNode('scm')
    scm.appendNode("connection", "scm:git:git://github.com/terl/lazysodium-java.git")
    scm.appendNode("developerConnection", "scm:git:ssh://github.com/terl/lazysodium-java")
    scm.appendNode("url", "http://github.com/terl/lazysodium-java")

    def license = root.appendNode('licenses').appendNode('license')
    license.appendNode('name', 'Mozilla Public License, Version 2.0')
    license.appendNode('url', 'http://www.mozilla.org/MPL/2.0/index.txt')
    license.appendNode('distribution', 'repo')

    def developer = root.appendNode('developers').appendNode('developer')
    developer.appendNode('name', 'Terl Tech Ltd')
    developer.appendNode('email', 'hello@terl.co')
}

jreleaser {
    signing {
        active = 'ALWAYS'
        mode = 'FILE'
        armored = true
        verify = true
        passphrase = findProperty("signing.password")
        publicKey = findProperty("key.public.path")
        secretKey = findProperty("key.private.path")
    }
    release {
        github {
            token = findProperty("github.token")
            repoOwner = "terl"
            host = 'github.com'
            username = 'gurpreet-'
            branch = 'master'
        }
    }
    deploy {
        maven {
            mavenCentral {
                'release-deploy' {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    applyMavenCentralRules = true
                    stagingRepository('build/staging-deploy')
                    username = getSonatypeUserName()
                    password = getSonatypePassword()
                    namespace = 'com.goterl'
                }
            }
            nexus2 {
                'snapshot-deploy' {
                    active = 'SNAPSHOT'
                    url = 'https://central.sonatype.com/repository/maven-snapshots'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository('build/staging-deploy')
                    username = getSonatypeUserName()
                    password = getSonatypePassword()
                }
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project.ext.groupId
            artifactId project.ext.artifactId
            version project.ext.version
            artifact sourcesJar
            artifact javadocJar

            pom.withXml createPomClosure
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

// Force character encoding in case the workspace was not set up correctly
tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    // a collection to track failedTests
    ext.failedTests = []

    afterTest { descriptor, result ->
        if (result.resultType == TestResult.ResultType.FAILURE) {
            String failedTest = "${descriptor.className}::${descriptor.name}"
            logger.debug("Adding " + failedTest + " to failedTests...")
            failedTests << [failedTest]
        }
    }

    afterSuite { suite, result ->
        if (!suite.parent) { // will match the outermost suite
            // logs each failed test
            if (!failedTests.empty) {
                logger.lifecycle("Failed tests:")
                failedTests.each { failedTest ->
                    logger.lifecycle("${failedTest}")
                }
            }
        }
    }
}